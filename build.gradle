group 'com.nongfenqi'
version '1.0-SNAPSHOT'
apply plugin: 'java'

sourceCompatibility = 1.8


configurations {
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile {
        extendsFrom pluginLibs
    }
}


repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.rundeck', name: 'rundeck-core', version: '2.9.2'
    pluginLibs group: 'org.freemarker', name: 'freemarker', version: '2.3.20'
}
// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Version': '1.2', 'Rundeck-Plugin-Archive': 'true', 'Rundeck-Plugin-File-Version': "${project.version}"
        def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')
        attributes 'Rundeck-Plugin-Classnames': 'com.nongfenqi.rundeck.plugins.dingtalk.DingtalkNotificationPlugin', 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)
//
//task wrapper(type: Wrapper) {
//    gradleVersion = '1.10'
//}